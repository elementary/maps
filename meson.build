project(
    'com.github.ryonakano.atlas',
    'vala', 'c',
    version: '0.1.0',
    meson_version: '>=0.49.0'
)

i18n = import('i18n')
add_global_arguments(
    '-DGETTEXT_PACKAGE="@0@"'.format(meson.project_name()),
    language:'c'
)

conf_data = configuration_data()
conf_data.set_quoted('LOCALEDIR', get_option('prefix') / get_option('localedir'))
conf_data.set_quoted('PKGDATADIR', get_option('prefix') / get_option('datadir'))
conf_data.set_quoted('PROJECT_NAME', meson.project_name())
config_header = configure_file(
    input: 'src' / 'config.vala.in',
    output: 'config.vala',
    configuration: conf_data
)

sources = files(
    'src' / 'Application.vala',
    'src' / 'GeoClue.vala',
    'src' / 'LocationMarker.vala',
    'src' / 'MainWindow.vala',
    'src' / 'Spinner.vala'
)

executable(
    meson.project_name (),
    config_header,
    sources,
    dependencies: [
        dependency('champlain-0.12'),
        dependency('champlain-gtk-0.12'),
        dependency('clutter-1.0'),
        dependency('geocode-glib-1.0'),
        dependency('granite', version: '>=5.4.0'),
        dependency('gtk+-3.0'),
        dependency('libgeoclue-2.0'),
        dependency('libhandy-1')
    ],
    install: true
)

subdir('data')
subdir('po')

meson.add_install_script('meson' / 'post_install.py')
